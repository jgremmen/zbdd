plugins {
  id 'java-library'
  id 'com.adarshr.test-logger' version '4.0.0'
}


description = 'Java Implementation of Shin Ichi Minato\'s Zero-Suppressed BDDs'
group = 'de.sayayi.lib'
version = libs.versions.zbdd.get()


layout.buildDirectory = '.build'


java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(11)
  }
}


javadoc {
  // javadoc 8 has various problems -> use javadoc 17
  javadocTool = javaToolchains.javadocToolFor {
    languageVersion = JavaLanguageVersion.of(17)
  }
}


test {
  useJUnitPlatform()
}


testlogger {
  theme = 'mocha'
  slowThreshold = 3500
}


repositories {
  mavenCentral()
}


dependencies {
  // java
  compileOnlyApi libs.jetbrains.annotations

  // test
  testImplementation libs.junit.jupiter.api
  testImplementation libs.junit.jupiter.params
  testRuntimeOnly libs.junit.jupiter.engine
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}


def resolvedJetbrainsAnnotationsVersion = configurations
    .compileClasspath
    .resolvedConfiguration
    .resolvedArtifacts
    .find { it.name == 'annotations' }
    .moduleVersion.id.version


javadoc {
  title "Java ZBDD Library ${version}"

  (options as StandardJavadocDocletOptions).with {
    linksOffline "https://javadoc.io/doc/org.jetbrains/annotations/${resolvedJetbrainsAnnotationsVersion}/",
                 'gradle/javadocs/jetbrains-annotations'

    encoding 'UTF-8'

    group('ZBDD Core API', ['de.sayayi.lib.zbdd'])
    group('ZBDD Cache API (Experimental)', ['de.sayayi.lib.zbdd.cache'])

    noQualifiers 'java.util', 'java.lang'

    addStringOption('Xdoclint:-missing', '-quiet')
  }

  failOnError false
}


tasks.withType(Jar).configureEach {
  reproducibleFileOrder true

  manifest {
    attributes(
        'Implementation-Vendor': 'Jeroen Gremmen',
        'Implementation-Vendor-Id': 'de.sayayi',
        'Implementation-Title': project.name,
        'Implementation-Version': project.version,
        'Created-By': "${System.getProperty('java.version')} (${System.getProperty('java.specification.vendor')})",
        'Built-Date': new Date().format('yyyy-MM-dd HH:mm:ss')
    )
  }

  from files('LICENSE')
}


jar {
  dependsOn test
}


apply from: 'gradle/publish.gradle'
apply from: 'gradle/changelog.gradle'


clean {
  delete layout.projectDirectory.dir('build')
}


wrapper {
  distributionType = Wrapper.DistributionType.BIN
}
