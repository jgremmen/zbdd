plugins {
  id 'java-library'
  id 'maven-publish'
  id 'signing'
}


description 'Java Implementation of Shin Ichi Minato\'s Zero-Suppressed BDDs'
group 'de.sayayi.lib'
version '0.2.2'


layout.buildDirectory = '.build'


def jetbrainsAnnotationsVersion = '[24.0,26.1)'
def junitVersion = '5.11.+'


java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(8)
  }
}


test {
  useJUnitPlatform()
}


repositories {
  mavenCentral()
}


dependencies {
  // java
  compileOnlyApi "org.jetbrains:annotations:${jetbrainsAnnotationsVersion}"

  // test
  testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
  testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}


def resolvedJetbrainsAnnotationsVersion = configurations
    .compileClasspath
    .resolvedConfiguration
    .resolvedArtifacts
    .find { it.name == 'annotations' }
    .moduleVersion.id.version


javadoc {
  title "Java ZBDD Library ${version}"

  (options as StandardJavadocDocletOptions).with {
    linksOffline "https://javadoc.io/doc/org.jetbrains/annotations/${resolvedJetbrainsAnnotationsVersion}/",
                 'gradle/javadocs/jetbrains-annotations'

    encoding 'UTF-8'

    group('ZBDD Core API', ['de.sayayi.lib.zbdd'])
    group('ZBDD Cache API (Experimental)', ['de.sayayi.lib.zbdd.cache'])

    noQualifiers 'java.util', 'java.lang'

    addStringOption('Xdoclint:-missing', '-quiet')
  }

  failOnError false

  // javadoc 8 has various problems -> use javadoc 17
  javadocTool = javaToolchains.javadocToolFor {
    languageVersion = JavaLanguageVersion.of(17)
  }
}


tasks.withType(Jar).configureEach {
  reproducibleFileOrder true

  manifest {
    attributes(
        'Implementation-Vendor': 'Jeroen Gremmen',
        'Implementation-Vendor-Id': 'de.sayayi',
        'Implementation-Title': project.name,
        'Implementation-Version': project.version,
        'Created-By': "${System.getProperty('java.version')} (${System.getProperty('java.specification.vendor')})",
        'Built-Date': new Date().format('yyyy-MM-dd HH:mm:ss')
    )
  }

  from files('LICENSE')
}


tasks.register('javadocJar', Jar) {
  archiveClassifier.set('javadoc')
  from javadoc
}


tasks.register('sourcesJar', Jar) {
  dependsOn classes

  archiveClassifier.set('sources')
  from sourceSets.main.allSource
}


publishing {
  publications {
    create('maven', MavenPublication) {
      from components.java

      artifact javadocJar
      artifact sourcesJar

      pom { MavenPom pom ->
        pom.name = 'Java ZBDD Library'
        pom.description = "Java Implementation of Shin Ichi Minato's Zero-Suppressed BDDs"
        pom.url = 'https://github.com/jgremmen/zbdd'
        pom.inceptionYear = '2021'

        licenses {
          license { MavenPomLicense mpl ->
            mpl.name = 'Apache License, Version 2.0'
            mpl.url = 'https://www.apache.org/licenses/LICENSE-2.0'
            mpl.distribution = 'repo'
          }
        }

        developers {
          developer { MavenPomDeveloper mpd ->
            mpd.id = 'jgremmen'
            mpd.name = 'Jeroen Gremmen'
            mpd.email = 'jeroen.gremmen@sayayi.de'
          }
        }

        scm { MavenPomScm scm ->
          scm.connection = 'scm:git:git://github.com/jgremmen/zbdd.git'
          scm.developerConnection = 'scm:git:git://github.com/jgremmen/zbdd.git'
          scm.url = 'https://github.com/jgremmen/zbdd'
        }
      }
    }
  }

  repositories {
    maven {
      name = 'mavenTemp'
      url = layout.buildDirectory.dir('repository')
    }
    maven {
      // maven central
      url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
      credentials {
        username = project.findProperty('mavenCentral.username')
        password = project.findProperty('mavenCentral.password')
      }
    }
  }
}


signing {
  sign publishing.publications.maven
}
