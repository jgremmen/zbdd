/*
 * Copyright 2025 Jeroen Gremmen
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

//file:noinspection GroovyAssignabilityCheck

apply plugin: 'org.danilopianini.publish-on-central'


def snapshot = version.toString().endsWith('-SNAPSHOT')


zipMavenCentralPortalPublication.dependsOn publishOSSRHPublicationToProjectLocalRepository


if (snapshot)
{
  publishing {
    repositories {
      maven {
        name = 'mavenCentralSnapshot'
        url = 'https://central.sonatype.com/repository/maven-snapshots/'

        mavenContent {
          snapshotsOnly()
        }

        credentials {
          username = rootProject.findProperty('mavenCentralPortalUsername')
          password = rootProject.findProperty('mavenCentralPortalPassword')
        }
      }
    }
  }
}


publishOnCentral {
  repoOwner = 'jgremmen'

  projectLongName = 'Java ZBDD Library'
  projectDescription = project.description

  licenseName = 'Apache License, Version 2.0'
  licenseUrl = 'https://www.apache.org/licenses/LICENSE-2.0'
}


publishing {
  publications {
    withType(MavenPublication).configureEach {
      pom {
        inceptionYear = '2021'

        developers {
          developer {
            id = 'jgremmen'
            name = 'Jeroen Gremmen'
            email = 'jeroen.gremmen@sayayi.de'
          }
        }
      }
    }
  }
}


tasks.register('publishToProjectLocalRepository') {
  group = PublishingPlugin.PUBLISH_TASK_GROUP
  description = 'Create a local repository with releasable artifacts.'

  dependsOn publishOSSRHPublicationToProjectLocalRepository

  outputs.dir layout.buildDirectory.dir('project-local-repository')
}


tasks.register('release-zbdd-artifacts') {
  group = 'release'

  inputs.property('snapshot', snapshot)

  dependsOn 'publishToProjectLocalRepository'

  if (snapshot)
  {
    description = "Release Zbdd snapshot artifacts to Maven Central Repository."

    dependsOn 'publishOSSRHPublicationToMavenCentralSnapshotRepository'
  }
  else
  {
    description = "Release Zbdd artifacts to Maven Central Repository."

    dependsOn 'zipMavenCentralPortalPublication'
    dependsOn 'validateMavenCentralPortalPublication'
    dependsOn 'releaseMavenCentralPortalPublication'
  }
}


tasks.named('zipMavenCentralPortalPublication') {
  mustRunAfter 'publishToProjectLocalRepository'
}
